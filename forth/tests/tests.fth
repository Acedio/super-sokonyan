REQUIRE std.fth

REQUIRE snes-std.fth
REQUIRE tests/snes-test-util.fth
REQUIRE tests/test-util.fth

: TEST-STACK-OPS
  T{ 1 2 NIP -> 2 }T
  T{ 1 2 OVER -> 1 2 1 }T
  T{ 1 2 TUCK -> 2 1 2 }T
  T{ 1 2 3 ROT -> 2 3 1 }T
  T{ 1 2 3 -ROT -> 3 1 2 }T
  T{ 1 >R 2 R@ R> -> 2 1 1 }T
  T{ 1 2 2>R 2R@ 2R> -> 1 2 1 2 }T
  T{ $123456 A.>R A.R> -> $123456 }T
;

: MY-CASE CASE 1 OF 111 ENDOF 2 OF 222 ENDOF >R 333 R> ENDCASE ;

: TEST-CONTROL-OPS
  T{ 1 BEGIN DUP 1+ DUP 3 = UNTIL -> 1 2 3 }T
  T{ 1 BEGIN DUP 3 < WHILE DUP 1+ REPEAT -> 1 2 3 }T
  T{ 0 4 1 DO I + LOOP -> 6 }T
  T{ 4 4 ?DO 1 LOOP -> }T
  T{ 1 TRUE IF 2 + THEN -> 3 }T
  T{ 2 FALSE IF 2 + THEN -> 2 }T
  T{ TRUE IF 1 ELSE 2 THEN -> 1 }T
  T{ FALSE IF 1 ELSE 2 THEN -> 2 }T
  T{ 1 MY-CASE 2 MY-CASE 4 MY-CASE -> 111 222 333 }T
  T{ TRUE CASE TRUE OF 111 ENDOF TRUE OF 222 ENDOF ENDCASE -> 111 }T
;

: TEST-MATH-OPS
  T{ 0 1 + -> 1 }T
  T{ -1 -2 - -> 1 }T
  T{ 0x00 0x01 OR -> 1 }T
  T{ 0x05 0x03 AND -> 1 }T
  T{ 0x03 0x02 XOR -> 1 }T
  T{ 0xFFFE INVERT -> 1 }T
  T{ 0xFFFF NEGATE -> 1 }T
  T{ 0xFF00 LSR -> 0x7F80 }T
  T{ 0xFF00 2/ -> 0xFF80 }T
  T{ 0x7F80 2* -> 0xFF00 }T
  T{ 0x1234 HIBYTE -> 0x0012 }T
  T{ 0x1234 SWAPBYTES -> 0x3412 }T
;

: TEST-COMPARISON-OPS
  T{  1  2 <  -> TRUE }T
  T{  2  2 <  -> FALSE }T
  T{ -1  1 <  -> TRUE }T
  T{  1 -1 <  -> FALSE }T
  T{  2  1 >  -> TRUE }T
  T{  2  2 >  -> FALSE }T
  T{  1 -1 >  -> TRUE }T
  T{ -1  1 >  -> FALSE }T
  T{  1  2 <= -> TRUE }T
  T{  2  2 <= -> TRUE }T
  T{  3  2 <= -> FALSE }T
  T{ -1  1 <= -> TRUE }T
  T{  1 -1 <= -> FALSE }T
  T{  2  1 >= -> TRUE }T
  T{  2  2 >= -> TRUE }T
  T{  2  3 >= -> FALSE }T
  T{  1 -1 >= -> TRUE }T
  T{ -1  1 >= -> FALSE }T
  T{  1  1 =  -> TRUE }T
  T{  0  1 =  -> FALSE }T
  T{  0  1 <> -> TRUE }T
  T{  1  1 <> -> FALSE }T
  T{ 0x0001 0xFFFF U< -> TRUE }T
  T{ 0xFFFF 0x0001 U< -> FALSE }T
  T{ 0xFFFF 0xFFFF U< -> FALSE }T
  T{ 0x0001 0xFFFF U<= -> TRUE }T
  T{ 0xFFFF 0x0001 U<= -> FALSE }T
  T{ 0xFFFF 0xFFFF U<= -> TRUE }T
  T{ 0xFFFF 0x0001 U> -> TRUE }T
  T{ 0x0001 0xFFFF U> -> FALSE }T
  T{ 0xFFFF 0xFFFF U> -> FALSE }T
  T{ 0xFFFF 0x0001 U>= -> TRUE }T
  T{ 0x0001 0xFFFF U>= -> FALSE }T
  T{ 0xFFFF 0xFFFF U>= -> TRUE }T

  \ Test overflowing signed comparisons.
  T{ 0x8000 1 < -> TRUE }T
  T{ 1 0x8000 < -> FALSE }T
  T{ -1 0x7FFF < -> TRUE }T
  T{ 0x7FFF -1 < -> FALSE }T
  T{ 1 0x8000 > -> TRUE }T
  T{ 0x8000 1 > -> FALSE }T
  T{ 0x7FFF -1 > -> TRUE }T
  T{ -1 0x7FFF > -> FALSE }T
  T{ 0x8000 1 <= -> TRUE }T
  T{ 1 0x8000 <= -> FALSE }T
  T{ 0x8000 0x8000 <= -> TRUE }T
  T{ -1 0x7FFF <= -> TRUE }T
  T{ 0x7FFF -1 <= -> FALSE }T
  T{ 1 0x8000 >= -> TRUE }T
  T{ 0x8000 1 >= -> FALSE }T
  T{ 0x8000 0x8000 >= -> TRUE }T
  T{ 0x7FFF -1 >= -> TRUE }T
  T{ -1 0x7FFF >= -> FALSE }T
;

: TEST-LITERALS
  T{ $123456 -> 0x0012 0x3456 }T
;

CREATELOWRAM TEST-LOWRAM-VAR
BANK@ LOWRAM BANK! 1 CELLS ALLOT BANK!
CREATE TEST-VAR 1 CELLS ALLOT

: TEST-VARS
  T{ 21 TEST-LOWRAM-VAR ! TEST-LOWRAM-VAR @ -> 21 }T
  T{ 42 TEST-LOWRAM-VAR ! TEST-LOWRAM-VAR @ -> 42 }T
;

\ Lowram should still be accessible in different banks.
: TEST-BANKS
  T{ BANK@ >R -> }T
  T{ 0 BANK! 33 TEST-LOWRAM-VAR ! 55 TEST-VAR ! -> }T 
  T{ LOWRAM BANK! TEST-LOWRAM-VAR @ TEST-VAR @ -> 33 55 }T
  T{ R> BANK! -> }T
;

: MY-CONSTANT CREATE , DOES> @ ;
21 MY-CONSTANT TEST-MY-CONSTANT 

: TEST-DOES
  T{ TEST-MY-CONSTANT TEST-MY-CONSTANT -> 21 21 }T
;

: TEST-CSWAP
  T{ 0x1234 TEST-LOWRAM-VAR ! -> }T
  T{ TEST-LOWRAM-VAR TEST-LOWRAM-VAR 1+ CSWAP! TEST-LOWRAM-VAR @ -> 0x3412 }T
;

: LUA-ONLY-TESTS
  TEST-BANKS \ Not implemented on the SNES yet.
;

: SNES-ONLY-TESTS
  T{ 0x4000 0x40 PPU-MULT -> 0x0000 0x10 }T
;

: SHARED-TESTS
  TEST-STACK-OPS
  TEST-CONTROL-OPS
  TEST-MATH-OPS
  TEST-COMPARISON-OPS
  TEST-LITERALS
  TEST-VARS
  TEST-DOES
  TEST-CSWAP
;

: SNES-MAIN
  SNES-TEST-INIT

  SHARED-TESTS
  SNES-ONLY-TESTS

  SNES-TESTS-PASSED-YAY!
;

SHARED-TESTS
LUA-ONLY-TESTS

: HURRAY! ." All tests passed!" CR ;
HURRAY!
